import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id 'idea'
    id 'java'
    id 'jacoco'
}

group = projectGroupId
version = "${projectVersion}.${majorVersion}.${minorVersion}.${patchId}"


repositories {
    mavenCentral()
}

ext {
    lombok_version = "1.18.26"
    slf4j_version = "2.0.7"
    vertx_version = "4.4.4"

    otl_version = "1.25.0"
    micrometer_version = "1.10.6"
    ws_rs_version = "2.1.1"
    jersey_version = "2.25.1"
    guava_version = "32.1.1-jre"
    curator_version = "5.5.0"
    jackson_version = "2.15.1"
    jakarta_validation_version = "3.0.2"
    jakarta_ws_version = "3.1.0"
    commons_lang_version = "3.11"

    hibernate_validator_version = "8.0.1.Final"
}

sourceSets {
    testE2E {
        java.srcDir "$projectDir/src/testE2E/java"
        resources.srcDir "$projectDir/src/testE2E/resources"
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    testE2EImplementation.extendsFrom testImplementation
    testE2ERuntime.extendsFrom testRuntime
}

dependencies {
    // TODO: Get rid of these unnecessary constraints. We are probably using it wrong. Need to check.
    constraints {
        implementation("org.slf4j:slf4j-api:$slf4j_version")
        implementation('org.apache.logging.log4j:log4j-slf4j2-impl:2.20.0')
        implementation('org.apache.logging.log4j:log4j-core:2.20.0')
        implementation("org.apache.commons:commons-lang3:$commons_lang_version")

        compileOnly("org.projectlombok:lombok:$lombok_version")
        annotationProcessor("org.projectlombok:lombok:$lombok_version")

        implementation("com.fasterxml.jackson.core:jackson-databind:$jackson_version")
        implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jackson_version")

        implementation("io.vertx:vertx-core:$vertx_version")
        implementation("io.vertx:vertx-config:${vertx_version}")
        implementation("io.vertx:vertx-config-yaml:${vertx_version}")
        implementation("io.vertx:vertx-web:$vertx_version")
        implementation("io.vertx:vertx-auth-common:$vertx_version")
        implementation("io.vertx:vertx-auth-jwt:$vertx_version")
        implementation("io.vertx:vertx-opentelemetry:$vertx_version")
        implementation("io.vertx:vertx-opentracing:$vertx_version")
        implementation("io.vertx:vertx-micrometer-metrics:$vertx_version")
        implementation("io.micrometer:micrometer-registry-prometheus:1.12.0")
        runtimeOnly('io.netty:netty-resolver-dns-native-macos:4.1.91.Final')


        implementation("io.opentelemetry:opentelemetry-api:$otl_version")
        implementation("io.opentelemetry:opentelemetry-sdk:$otl_version")
        implementation("io.opentelemetry:opentelemetry-exporter-logging:$otl_version")
        implementation("io.opentelemetry:opentelemetry-exporter-otlp:$otl_version")
        implementation("io.opentelemetry:opentelemetry-semconv:$otl_version-alpha")

        implementation("io.micrometer:micrometer-core:$micrometer_version")
        implementation("io.micrometer:micrometer-registry-otlp:$micrometer_version")
        implementation("org.apache.curator:curator-framework:$curator_version")

        // TODO: remove the jmx option altogether. For that need to unify the tracing and metering to use otlp exporters
        //  by default, and have the docker compose setup ready for local setup.
        implementation("io.micrometer:micrometer-registry-jmx:$micrometer_version")
        implementation("com.google.guava:guava:$guava_version")

        implementation("jakarta.validation:jakarta.validation-api:$jakarta_validation_version")

        testCompileOnly("org.projectlombok:lombok:$lombok_version")
        testAnnotationProcessor("org.projectlombok:lombok:$lombok_version")
        testImplementation('org.junit.jupiter:junit-jupiter:5.9.1')
        testImplementation('org.mockito:mockito-junit-jupiter:5.4.0')
        testImplementation("io.vertx:vertx-junit5:$vertx_version")
        testImplementation("io.vertx:vertx-web-client:$vertx_version")
        testImplementation("org.apache.curator:curator-test:$curator_version")

        testE2EImplementation("javax.ws.rs:javax.ws.rs-api:$ws_rs_version")
        testE2EImplementation("org.glassfish.jersey.core:jersey-client:$jersey_version")
        testE2EImplementation("org.glassfish.jersey.media:jersey-media-json-jackson:$jersey_version")
    }

    // code generation
    compileOnly("org.projectlombok:lombok")
    annotationProcessor("org.projectlombok:lombok")

    // common apis
    implementation("com.google.guava:guava")
    implementation("org.slf4j:slf4j-api")
    implementation("io.opentelemetry:opentelemetry-api")
    implementation("jakarta.validation:jakarta.validation-api")

    // common test framework
    testCompileOnly("org.projectlombok:lombok")
    testAnnotationProcessor("org.projectlombok:lombok")
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation('org.mockito:mockito-junit-jupiter')
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.register('testE2E', Test) {
    testClassesDirs = sourceSets.testE2E.output.classesDirs
    classpath = sourceSets.testE2E.runtimeClasspath
    outputs.upToDateWhen { false } //this will force testE2E to execute always on target invocation.
}

tasks.register('copyDependencies', Copy) {
    into layout.buildDirectory.dir('dependencies')
    from configurations.runtimeClasspath
}

tasks.withType(JavaCompile).configureEach {
    options.release.set(17)
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add("-parameters")
}

tasks.withType(JacocoReport).configureEach {
    reports {
        html.required = true
        xml.required = true
    }
    getSourceDirectories().from(sourceSets.main.allSource.srcDirs)
    getClassDirectories().from(sourceSets.main.output)
    getExecutionData().from(fileTree(buildDir).include("/jacoco/*.exec"))
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    testLogging {
        events "PASSED", "SKIPPED", "FAILED", "STANDARD_OUT", "STANDARD_ERROR"
        exceptionFormat TestExceptionFormat.FULL
        showCauses true
        showExceptions true
        showStackTraces true
        showStandardStreams false
    }
    afterSuite { desc, result ->
        if (!desc.parent) { // will match the outermost suite
            def output = "Test Sumary: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            def startItem = '|  ', endItem = '  |'
            def repeatLength = startItem.length() + output.length() + endItem.length()
            println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
        }
    }
}
