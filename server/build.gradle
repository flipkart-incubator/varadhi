import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id "com.flipkart.varadhi.java-application-conventions"
    id "jacoco-report-aggregation"
}

dependencies {

    implementation(project(":common"))
    implementation(project(":core"))
    implementation(project(":producer"))
    implementation(project(":entities"))
    implementation(project(":authz"))
    implementation(project(":controller"))
    implementation(project(":consumer"))

    implementation("org.apache.logging.log4j:log4j-slf4j2-impl")
    implementation("org.apache.logging.log4j:log4j-core")
    implementation("org.apache.commons:commons-lang3")

    implementation("com.fasterxml.jackson.core:jackson-databind")

    implementation("io.vertx:vertx-core")
    implementation("io.vertx:vertx-config")
    implementation("io.vertx:vertx-config-yaml")
    implementation("io.vertx:vertx-web")
    implementation("io.vertx:vertx-auth-common")
    implementation("io.vertx:vertx-auth-jwt")
    implementation("io.vertx:vertx-zookeeper")
    implementation("io.vertx:vertx-opentelemetry")
    implementation("io.vertx:vertx-opentracing")
    implementation("io.vertx:vertx-micrometer-metrics")
    implementation("io.micrometer:micrometer-registry-prometheus")

    // TODO: check why still getting warning on class not found.
    if (DefaultNativePlatform.getCurrentOperatingSystem().isMacOsX()) {
        runtimeOnly("io.netty:netty-resolver-dns-native-macos:4.1.114.Final:osx-x86_64")
    }

    implementation("io.opentelemetry:opentelemetry-sdk")
    implementation("io.opentelemetry:opentelemetry-exporter-logging")
    implementation("io.opentelemetry:opentelemetry-exporter-otlp")
    implementation("io.opentelemetry:opentelemetry-exporter-prometheus")
    implementation("io.opentelemetry.semconv:opentelemetry-semconv")

    implementation("io.micrometer:micrometer-registry-otlp")
    implementation("io.micrometer:micrometer-registry-jmx")

    implementation("dev.failsafe:failsafe")

    //For ZK persistence, curator-framework.
    implementation("org.apache.curator:curator-framework")
    runtimeOnly(project(":pulsar"))

    testImplementation("io.vertx:vertx-zookeeper:4.5.1")

    testImplementation(project(":pulsar"))
    testImplementation(testFixtures(project(":spi")))
    //use like this where ever reequired
    testImplementation(testFixtures(project(":entities")))
    testImplementation(testFixtures(project(":common")))
    testImplementation("org.apache.curator:curator-test")
    testImplementation("io.vertx:vertx-junit5")
    testImplementation("io.vertx:vertx-web-client")

    testE2EImplementation("org.glassfish.jersey.core:jersey-client:$jersey_version")
    testE2EImplementation("org.glassfish.jersey.media:jersey-media-json-jackson:$jersey_version")

    testE2ERuntime("org.jacoco:org.jacoco.agent:$jacoco_version:runtime")


    // jacoco aggregation dependency -- explicit inclusion.
    jacocoAggregation project(":pulsar")

}

reporting {
    reports {
        combinedJacocoReport(JacocoCoverageReport) {
            testType = TestSuiteType.UNIT_TEST
        }
    }
}

tasks.register('copyMainConfig', Copy.class, copyConfigForModule('main'))
tasks.register('copyTestConfig', Copy.class, copyConfigForModule('test'))

compileTestE2EJava {
    dependsOn tasks.named('copyTestConfig')
}

application {
    mainClass = "com.flipkart.varadhi.VaradhiApplication"
}

run {
    dependsOn "copyMainConfig"
    args = ["$buildDir/resources/main/configuration.yml"]
    jvmArgs = [
            "-Dvertx.disableDnsResolver=true",
            "-Dlog4j.configurationFile=$buildDir/resources/main/log4j2.xml"
    ]
}

testE2E {
    useJUnitPlatform()
    jvmArgs '--add-opens', 'java.base/java.net=ALL-UNNAMED'
}
